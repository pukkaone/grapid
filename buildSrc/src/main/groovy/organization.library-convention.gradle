plugins {
  id 'checkstyle'
  id 'java-library'
  id 'maven-publish'
  id 'signing'
}

group = 'com.github.pukkaone'

repositories {
  mavenLocal()
  jcenter()
}

ext {
  lombokVersion = '1.18.18'
  springBootVersion = '2.3.9.RELEASE'
  springFrameworkVersion = '5.2.13.RELEASE'
}

dependencies {
  testImplementation 'org.assertj:assertj-core:3.16.1'
  testImplementation 'org.junit.jupiter:junit-jupiter:5.6.3'
  annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
  compileOnly "org.projectlombok:lombok:${lombokVersion}"
}

sourceCompatibility = JavaVersion.VERSION_11

java {
  withJavadocJar()
  withSourcesJar()
}

checkstyle {
  toolVersion = '8.29'
}

test {
  useJUnitPlatform()
}

jar {
  manifest {
    attributes(
        'Automatic-Module-Name': "${project.group}.${project.name}".replace('-', '.'),
        'Implementation-Version': project.version)
  }
}

publishing {
  publications {
    library(MavenPublication) {
      from components.java
      pom {
        name = project.name
        afterEvaluate {
          description = project.description
        }
        url = 'https://github.com/pukkaone/grapid'

        licenses {
          license {
            name = 'Apache License, Version 2.0'
            url = 'https://www.apache.org/licenses/LICENSE-2.0'
          }
        }

        developers {
          developer {
            id = 'pukkaone'
            name = 'Chin Huang'
          }
        }

        scm {
          connection = 'scm:git:git@github.com:pukkaone/grapid.git'
          developerConnection = 'scm:git:git@github.com:pukkaone/grapid.git'
          url = 'https://github.com/pukkaone/grapid'
        }
      }
    }
  }

  repositories {
    maven {
      url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
      credentials {
        username = findProperty('sonatypeUsername') ?: 'UNKNOWN'
        password = findProperty('sonatypePassword') ?: 'UNKNOWN'
      }
    }
  }
}

signing {
  required { hasProperty('sonatypeUsername') && hasProperty('sonatypePassword') }
  sign publishing.publications.library
}
