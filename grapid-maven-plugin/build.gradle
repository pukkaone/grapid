description = 'Maven plugin that translates GraphQL schema to Java source code'

ext {
  mavenVersion = '3.6.0'
}

dependencies {
  implementation project(':grapid-compiler')
  implementation "org.apache.maven:maven-plugin-api:${mavenVersion}"
  compileOnly "org.apache.maven.plugin-tools:maven-plugin-annotations:${mavenVersion}"
  implementation 'org.apache.maven.shared:file-management:3.0.0'
}

task generatePluginDescriptor(type: Exec) {
  doFirst {
    pom {
      packaging = 'maven-plugin'
    }.withXml {
      asNode()
          .appendNode('build')
              .appendNode('outputDirectory', 'classes/java/main')
    }.writeTo("${buildDir}/pom.xml")
  }

  workingDir buildDir
  commandLine "${rootDir}/mvnw", '--batch-mode', '--errors', "org.apache.maven.plugins:maven-plugin-plugin:${mavenVersion}:descriptor"
}

jar.dependsOn(generatePluginDescriptor)

def installer = install.repositories.mavenInstaller
def deployer = uploadArchives.repositories.mavenDeployer
[installer, deployer]*.pom*.whenConfigured { pom ->
  pom.packaging = 'maven-plugin'
}
